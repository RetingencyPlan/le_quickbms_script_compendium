# Tekken Tag Tournament (PS2)

math ttt_ver = 0

get elf_name filename
if elf_name == "SLPS_200.15"
	# Japan (2000/03/30)
	callfunction read_elf 1
elif elf_name == "SLUS_200.01"
	# USA (2000/10/26)
	callfunction read_elf 1
elif elf_name == "SCES_500.01"
	# Europe (2000/11/24)
	callfunction read_elf 1
elif elf_name == "SCED_500.41"
	# United Kingdom/Great Britain/England - Official PlayStation 2 Magazine Issue 2 (2000/12/04)
	callfunction read_elf 1
elif elf_name == "SCPS_560.02"
	# South Korea (2002/02/22)
	callfunction read_elf 1
else
	print " wrong file(no, tekken.bin does not count). "
	cleanexit
endif

open FDSE elf_name 0
open FDSE "tekken.bin" 1

if ttt_ver = 1
	xmath ttt_info_entries "(ttt_info_offset_02 - ttt_info_offset_01) / 0xc"
elif ttt_ver = 2
	xmath ttt_info_entries "(ttt_info_offset_02 - ttt_info_offset_01) / 0x10"
endif

goto ttt_info_offset_01
for i = 0 < ttt_info_entries
	if ttt_ver = 1
		get file_offset long
		get file_size long
		get ttt_03 long
	elif ttt_ver = 2
		get file_offset long
		get file_size long
		get ttt_03 long
		get file_id long
	endif
	
	if i = 0
		math iso9660_base_offset = file_offset
	endif
	
	math file_offset - iso9660_base_offset
	math file_offset << 11
	
	if file_size == 0x3509
	elif file_size == 0x350a
	else
		math file_size - 0x350a
		log "" file_offset file_size 1
	endif
next i

startfunction read_elf
	idstring "\x7fELF"
	get elf_01 byte
	get elf_02 byte
	get elf_03 short
	getdstring elf_04 8
	get e_type short
	get e_machine short
	get e_version long
	get e_entry long
	get e_phoff long
	get e_shoff long
	get e_flags long
	get e_ehsize short
	get e_phentsize short
	get e_phnum short
	get e_shentsize short
	get e_shnum short
	get e_shstrndx short
	
	goto e_phoff
	if e_phnum == 5 # work-around for SCPS_560.02(korean exe of the game)
		getdstring p_dummy 8
	endif
	for ph = 0 < e_phnum
		get p_type long
		get p_offset long
		get p_vaddr long
		get p_filesz long
		get p_memsz long
		get p_flags long
		get p_align long
	next ph
	
	savepos elf_pos01
	
	goto e_shoff
	for sh = 0 < e_shnum
		get sh_name long
		get sh_type long
		get sh_flags long
		get sh_addr long
		get sh_offset long
		get sh_size long
		get sh_link long
		get sh_info long
		get sh_addralign long
		get sh_entsize long
	next sh
	
	goto elf_pos01
	get elf_05 long
	get elf_06 long
	get elf_07 long
	
	if elf_05 == 0x01b8b730 # Japan (1.30)
		math ttt_ver = 1
		math ttt_info_offset_01 = 0x1b9000
		math ttt_info_offset_02 = 0x1bb310
	elif elf_05 == 0x01b8bc30 # Japan (2.00)
		math ttt_ver = 1
		math ttt_info_offset_01 = 0x1b94b0
		math ttt_info_offset_02 = 0x1bb7c0
	elif elf_05 == 0x01bac8e8 # South Korea (1.01)
		math ttt_ver = 2
		math ttt_info_offset_01 = 0x1c7640
		math ttt_info_offset_02 = 0x1ca500
	elif elf_05 == 0x01bb1bb0 # Japan (3.00)
		math ttt_ver = 2
		math ttt_info_offset_01 = 0x1baf10
		math ttt_info_offset_02 = 0x1bddd0
	elif elf_05 == 0x01c8daf0 # Official PlayStation 2 Magazine Issue 2 (1.00)
		math ttt_ver = 2
		math ttt_info_offset_01 = 0x1caf60
		math ttt_info_offset_02 = 0x1cde20
	elif elf_05 == 0x01c8ddf0 # USA (1.00)
		math ttt_ver = 2
		math ttt_info_offset_01 = 0x1cad40
		math ttt_info_offset_02 = 0x1cdc00
	elif elf_05 == 0x01c8def0 # USA (2.00)
		math ttt_ver = 2
		math ttt_info_offset_01 = 0x1cae40
		math ttt_info_offset_02 = 0x1cdd00
	elif elf_05 == 0x01c91df0 # Europe (1.00, 2.00)
		math ttt_ver = 2
		math ttt_info_offset_01 = 0x1cf1c0
		math ttt_info_offset_02 = 0x1d2080
	endif
endfunction
