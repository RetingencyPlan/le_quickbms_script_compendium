# SCEE London Studio/Team Soho/SCEE - *.PAK files

math INFO_OFF1 == 0x190

getdstring ARCHIVE_TYPE 8
if ARCHIVE_TYPE != "SceeWhPC" && ARCHIVE_TYPE != "SceeWhPk"
	break
endif
getdstring TIMESTAMP 20
get TABLE_SIZE long
get TOTAL_FILES long
getdstring RANDOM 0x80
for t1 = 0 < 28
	get DUMMY03 long
next t1
for t2 = 1 < 0x20
	getdstring EXT01 4
	putarray 10 t2 EXT01
next t2

goto INFO_OFF1
get FOLDERS1 long
get SIZE1 long
if FOLDERS1 != 0 && SIZE1 != 0
	# copy-pasted straight from aluigi's getaway.bms script
	savepos INFO_OFF2
	xmath FOLDERS2 "FOLDERS1 * 0x74"
	xmath INFO_OFF2 "INFO_OFF2 + FOLDERS2"

    for FOLDER = 0 < FOLDERS1

        getdstring EXT2 4
        getdstring NAME2 0x60
        get ENTRIES1 long
        get ENTRIES2 long
        get ENTRIES3 long
        get OFFSET2 long

        savepos TMP1
        math OFFSET2 + INFO_OFF2
        goto OFFSET2
		
		if ENTRIES2 == 0 || ENTRIES2 == 1
			if ENTRIES1 > ENTRIES3
				math ENTRIES == ENTRIES1
			elif ENTRIES1 < ENTRIES3
				math ENTRIES == ENTRIES3
			elif ENTRIES1 == ENTRIES3
				math ENTRIES == ENTRIES3
			endif
		else
			xmath ENTRIES "(ENTRIES3 - ENTRIES2) + 1"
		endif

        string NAME2 + \
		math i2 = 0
        for i = 0 < ENTRIES
            get FSIZE2 long
            get FOFFSET2 long
            if ARCHIVE_TYPE == "SceeWhPC"
                get NAME_CRC2 long
            endif
			math i2 + 1
			
            math FOFFSET2 << 11
            if FSIZE2 != 0
				string NAME3 p "%s%d.%s" NAME2 i2 EXT2
                if FSIZE2 & 0xc0000000 # 0x40000000 and 0x80000000
					savepos TMP2
					goto FOFFSET2
					get FZSIZE1 long
					goto TMP2
                    math FSIZE2 & 0x3fffffff
					string TMP_NAME2 p "%s[compressed_0x%08x]" NAME3 FZSIZE1
                    log TMP_NAME2 FOFFSET2 FSIZE2
                else
                    log NAME3 FOFFSET2 FSIZE2
                endif
            endif
        next i

        goto TMP1

    next FOLDER
	
	xmath INFO_OFF3 "INFO_OFF2 + SIZE1"
	log MEMORY_FILE2 INFO_OFF3 TABLE_SIZE
else
	xmath INFO_OFF4 "INFO_OFF1 + 8"
	log MEMORY_FILE2 INFO_OFF4 TABLE_SIZE
endif

putvarchr MEMORY_FILE 0x100 0
log MEMORY_FILE 0 0x100

for
	get FOFFSET threebyte MEMORY_FILE2
	get NAME_POS byte MEMORY_FILE2
	get NAME_SIZE byte MEMORY_FILE2
	get DUMMY34 short MEMORY_FILE2
	get FSIZE long MEMORY_FILE2
	
	if ARCHIVE_TYPE == "SceeWhPC"
		get NAME_CRC long MEMORY_FILE2
	endif
	
	if FOFFSET == 0
	if NAME_POS == 0
	if NAME_SIZE == 0
	if DUMMY34 == 0
	if FSIZE == 0
		break
	endif
	endif
	endif
	endif
	endif
	
	math FOFFSET << 11
	
	savepos TMP2 MEMORY_FILE2
	savepos NAME_TMP1 MEMORY_FILE2
	math NAME_TMP1 + NAME_SIZE
	math NAME_TMP1 - 1
	goto NAME_TMP1 MEMORY_FILE2
	get EXT_IDX byte MEMORY_FILE2

	if EXT_IDX <= 0x20
		xmath FULL_NAME_SIZE "(NAME_POS + NAME_SIZE) - 1"
		getarray FILE_EXT 10 EXT_IDX
		math NAME_SIZE - 1
		goto TMP2 MEMORY_FILE2
		getdstring NAME NAME_SIZE MEMORY_FILE2
		get EXT_IDX2 byte MEMORY_FILE2
	else
		xmath FULL_NAME_SIZE "NAME_POS + NAME_SIZE"
		goto TMP2 MEMORY_FILE2
		getdstring NAME NAME_SIZE MEMORY_FILE2
	endif
	
	if NAME_POS != 0
		savepos MTMP1 MEMORY_FILE
		goto NAME_POS MEMORY_FILE
		putct NAME string -1 MEMORY_FILE
		goto MTMP1 MEMORY_FILE
	else
		goto 0 MEMORY_FILE
		putct NAME string -1 MEMORY_FILE
	endif
	
	goto 0 MEMORY_FILE
	getdstring FULL_NAME FULL_NAME_SIZE MEMORY_FILE
	
	if EXT_IDX <= 0x20
		string FNAME p "%s.%s" FULL_NAME FILE_EXT
	else
		string FNAME p "%s" FULL_NAME
	endif
	
	log FNAME FOFFSET FSIZE
next
