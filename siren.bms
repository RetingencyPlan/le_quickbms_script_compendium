# Siren/Forbidden Siren - siren.tbl/siren.###

get tbl_bname basename

open FDDE "tbl"
filexor 0xff
idstring "SDT4"
get files long
get tbl_size long
get tbl03 long # probably some kind of hash...
get info_off long
get names_off long
get tbl04 long # always 1
goto info_off
math tbl_arcnum = 0
math tbl_tempnum = 0
math tbl_arcsize = 0x20000000
for i = 0 < files
    get offset long
    get size long
    get name_off long
	
	if i = 0
		math tbl05 = offset
	endif
	
	math offset - tbl05
	math offset2 = offset
	xmath offset "(offset << 11) % tbl_arcsize"
	
    savepos tmp
    math name_off + names_off
    goto name_off
    get name string
    goto tmp
	
	xmath tbl_arcnum "offset2 / (tbl_arcsize >> 11)"
	
	string full_tbl_load p "%s.%03d" tbl_bname tbl_arcnum
	open fdse full_tbl_load 1
	
	xmath stbl_draft1 "size + offset"
	xmath stbl_draft2 "(stbl_draft1 / tbl_arcsize) + 1"
	xmath stbl_draft3 "stbl_draft2 - 1"
	
	filexor ""
	if stbl_draft1 u> tbl_arcsize
		math tbl_tempnum = 0
		append
		for xp = 0 < stbl_draft2
			xmath stbl_draft4 "(tbl_tempnum + xp) * tbl_arcsize"
			xmath stbl_draft5 "(stbl_draft4 / tbl_arcsize) + tbl_arcnum"
			string temp_tbl_load p "%s.%03d" tbl_bname stbl_draft5
			open fdse temp_tbl_load 2
			if xp = 0
				xmath size "tbl_arcsize - offset"
			else
				set offset long 0
				if xp == stbl_draft3
					xmath size "stbl_draft1 - stbl_draft4"
				else
					xmath size "tbl_arcsize % stbl_draft1"
				endif
			endif
			log name offset size 2
		next xp
		append
	else
		log name offset size 1
	endif
	filexor 0xff
next i
